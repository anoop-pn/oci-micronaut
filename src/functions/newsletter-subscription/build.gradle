plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.6.2"
    id 'org.checkerframework' version '0.6.20'
}

group = "microanut.mushop.newsletter.subscription"

repositories {
    mavenCentral()
    mavenLocal()
}

checkerFramework {
    checkers = [
            'edu.ucr.cs.riple.taint.ucrtainting.UCRTaintingChecker',
    ]
    extraJavacArgs = [
            '-Awarns',
            '-AstubDebug'
    ]
}

dependencies {
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-jackson-databind")
    implementation("io.micronaut:micronaut-runtime")
    implementation("jakarta.annotation:jakarta.annotation-api")
    implementation("io.micronaut:micronaut-http-client")
    implementation 'javax.mail:mail:1.5.0-b01'

    runtimeOnly("org.slf4j:slf4j-simple")
    implementation("io.micronaut:micronaut-validation")

    testImplementation("com.fnproject.fn:testing-junit4")
    testImplementation("io.micronaut:micronaut-http-client")

    checkerFramework 'edu.ucr.cs.riple.taint:ucrtainting-checker:0.1'
    implementation 'edu.ucr.cs.riple.taint:ucrtainting-checker:0.1'

}

application {
    mainClass.set("com.example.Application")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks {
    // the base image for Oracle Function is fnproject/fn-java-fdk:jre11-latest
    // TODO: build fnproject image on top of graalvm-ce
//    if (javaBaseImage == "graalvm") {
//        dockerfile {
//            baseImage = "ghcr.io/graalvm/graalvm-ce:ol8-java11-22"
//        }
//    }

    dockerfileNative {
        args("-XX:MaximumHeapSizePercent=80")
        instruction "RUN echo 'http://dl-cdn.alpinelinux.org/alpine/v3.11/main' >> /etc/apk/repositories && apk update && apk add 'zlib<1.2.12'"
    }

    dockerBuild {
        images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/$project.name-${javaBaseImage}:$project.version"]
    }

    dockerBuildNative {
        images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/${project.name}-native:$project.version"]
    }
}

graalvmNative.toolchainDetection = false
graalvmNative {
    binaries.configureEach {
        buildArgs.add("--initialize-at-build-time=newsletter.subscription")
        buildArgs.add("--static")
        buildArgs.add("--add-exports=org.graalvm.nativeimage.builder/com.oracle.svm.core.jni=ALL-UNNAMED")
    }
}

micronaut {
    runtime("oracle_function")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("newsletter.subscription.*")
    }
}
