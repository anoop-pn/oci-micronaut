plugins {
    id("com.github.johnrengelman.shadow") version "7.1.2"
    id("io.micronaut.application") version "3.6.2"
    id 'org.checkerframework' version '0.6.20'
}

group = "micronaut.mushop"

repositories {
    mavenCentral()
    mavenLocal()
}

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("mushop.orders.*")
    }
}

checkerFramework {
    checkers = [
            'edu.ucr.cs.riple.taint.ucrtainting.UCRTaintingChecker',
    ]
    extraJavacArgs = [
            '-Awarns',
            '-AstubDebug'
    ]
}

dependencies {
    annotationProcessor("io.micronaut.data:micronaut-data-processor")

    implementation(project(":app")) {
        exclude group:'com.h2database', module:'h2'
    }

    implementation("org.slf4j:jcl-over-slf4j")

    // Authentication
    implementation("software.amazon.awssdk:sts")

    // Configuration discovery
    implementation("io.micronaut.aws:micronaut-aws-secretsmanager")
    implementation("io.micronaut.aws:micronaut-aws-parameter-store")

    // Metrics
    implementation("io.micronaut.micrometer:micronaut-micrometer-registry-cloudwatch")

    // Database
    implementation("mysql:mysql-connector-java")

    testImplementation(project(":tck"))
    testImplementation("io.micronaut.nats:micronaut-nats")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:mysql")

    checkerFramework 'edu.ucr.cs.riple.taint:ucrtainting-checker:0.1'
    implementation 'edu.ucr.cs.riple.taint:ucrtainting-checker:0.1'
}

application {
    mainClass.set("mushop.orders.AwsApplication")
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

if (javaBaseImage == "graalvm") {
    dockerfile {
        baseImage = "ghcr.io/graalvm/graalvm-ce:ol8-java11-21.1.0"
    }
}

dockerBuild {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/$project.parent.name-$project.name-${javaBaseImage}:$project.version"]
}

dockerBuildNative {
    images = ["phx.ocir.io/oraclelabs/micronaut-showcase/mushop/${project.parent.name}-${project.name}-native:$project.version"]
}

shadowJar {
    zip64 = true
}

graalvmNative {
    binaries {
        main {
            buildArgs.add('--initialize-at-run-time=org.bouncycastle.asn1.ASN1UTF8String')
        }
    }
}

dockerfileNative {
    instruction "RUN echo 'http://dl-cdn.alpinelinux.org/alpine/v3.11/main' >> /etc/apk/repositories && apk update && apk add 'zlib<1.2.12'"
}
